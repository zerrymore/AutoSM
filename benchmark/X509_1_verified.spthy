
theory X509_1
begin

builtins: asymmetric-encryption, signing

functions: h/1

let Alice(skA, pkA, pkB) =        
    event Role_A(skA);
    new ta; new na; // The timestamp Ta and nonce Na are not used here.
    new xa; new Ya; // Xa and Ya are the data transmitted,
    let m = <ta,na,pkB,xa,aenc(Ya,pkB)> in
    let ha = h(m) in
    let msg = <pkA, m, ha, sign(ha, skA)> in
    event Running_A(pkA, pkB, <'A','B', xa, Ya>);
    event SecretA(pkA, pkB, Ya);
    event Out_A_1(msg);
    out(msg)


let Bob(skB, pkB, pkA)=      
    event Role_B(skB);
    in(cipher);
    let <x, m, ha, sig> = cipher in
    let =pkA = x in
    let h(=m) = ha in 
    let <ta, na, =pkB, xa, cipher> = m in 
    let Ya = adec(cipher, skB) in
    if verify(sig, ha, pkA) = true then
    (
        event Commit_B(x, pkB, <'A','B',xa, Ya>);
        event Secret(x, pkB, Ya) ) 

 process:
    !(
      new skA; out(pk(skA)); event HonestA(pk(skA));
      !(
        new skB; out(pk(skB)); event HonestB(pk(skB));
        (!Alice(skA, pk(skA), pk(skB))  |  !Bob(skB, pk(skB), pk(skA)))
      )
    )

// check the secrecy of a secret x protected by the long-term key of B
lemma Secrecy:
  "
    not(
      Ex A B m #i.
        Secret(A, B, m)@ #i
        // & SecretB(A, B, m)@ #j
        & (Ex #t. HonestA(A) @ t)
        & (Ex #t. HonestB(B) @ t)
        & (Ex #r. K(m) @ #r)
    )
  "

// check the authenticity and freshness of the transaction from B's point of view
lemma injectiveagreement_B:
   "All A B t #i.
     Commit_B(A,B,t) @i
     ==> (Ex #j. Running_A(A,B,t) @j
         & j < i
         & not (Ex A2 B2 #i2. Commit_B(A2,B2,t) @i2
                            & not (#i2 = #i)))
  "

// check the authenticity of the transaction from B's point of view
lemma agreement_B:
  "All A B t #i.
    Commit_B(A,B,t) @i
    ==> (Ex #j. Running_A(A,B,t) @j  & j < i)
  "

// sanity check: check if honest execution is possible
lemma Session_key_honest_setup:
  exists-trace
  " Ex A B t #i #j.
        Running_A(A, B, t) @ #i
      & Commit_B(A, B, t)@ #j
  "


//  ==============================================================================
// summary of summaries:

// analyzed: X509Sapic.spthy

//   processing time: 8.98s

//   WARNING: 1 wellformedness check failed!
//            The analysis results might be wrong!

//   Secrecy (all-traces): verified (17 steps)
//   injectiveagreement_B (all-traces): falsified - found trace (15 steps)
//   agreement_B (all-traces): verified (11 steps)
//   Session_key_honest_setup (exists-trace): verified (7 steps)

// ==============================================================================

end